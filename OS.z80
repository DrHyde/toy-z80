ORG 0x0000
INCLUDE "_constants.z80"
INCLUDE "_macros.z80"

rst0:
        DI
        IM 1
        LD HL, stack_top + 1
        JR init__part2   ; jump to immediately after RST block

osfunc: ; RST 0x08
        SWAPREGS
osfunc__in_osfunc equ $ + 1       ; first, see if we're already in OSFUNC
        LD A, 0                   ; this 0 is correct for the first call and will
                                  ; be modified by future calls and on exit
        CP 1
        JR Z, osfunc__recurse     ; we are, so crash
        INC A                     ; we're not. record that we are
        LD (osfunc__in_osfunc), A

    ; do osfunc
        POP HL                    ; address we want to return to
        LD (osfunc__return_address), HL
        POP HL                    ; address of the function to call
        JP (HL)                   ; stack contains arguments

osfunc__exit:                      ; OSFUNCs finish with a JP to here
        LD A, (osfunc__in_osfunc)  ; mark that we're no longer in OSFUNC
        DEC A
        LD (osfunc__in_osfunc), A
        SWAPREGS
osfunc__return_address equ $ + 1
        JP 0x0000                  ; gets modified to the address we want to return to
osfunc__recurse:                   ; ... print message ...
        JR osfunc__recurse

init__part2:
        LD SP, HL             ; initialise stack

; TODO: slurp all ports to clear data
; TODO: mount root fs

        ; start the clock
        LD A, 1
        OUT (port_hw_start_clock), A
        EI
        JR load_shell

ORG 0x0038                      ; RST 0x38
        JR 0x0038
        DEFW 0, 0, 0

load_shell:
        LD HL, shell_name
        PUSH HL
        LD HL, os_exec
        PUSH HL
        OSFUNC
        JR load_shell

        ; add B little-endian bytes from (HL) to (DE), destroys B, AF/DE/HL
        ; used by NMI so must be SUPER FAST, so always in memory
add_int:
        OR A          ; reset C
add_loop:
        LD A,(DE)
        ADC A,(HL)    ; add (HL) and carry flag to A and set carry flag if necessary
        LD (DE),A
        INC HL
        INC DE
        DJNZ add_loop
        RET

        ; sub B little-endian bytes at (HL) from (DE), destroys B, AF/DE/HL
sub_int:
        OR A
sub_loop:
        LD A,(DE)
        SBC A,(HL)
        LD (DE),A
        INC HL
        INC DE
        DJNZ sub_loop
        RET

ORG 0x0062

clock_32_bits:
        DEFW 0
        DEFW 0

; can be called while in OS code or in user code, so must preserve
; registers and stack!
nmi:
        PUSH AF
        LD A, (nmi__in_nmi)
        CP 0
        JR Z, nmi__chk_in_nmi__NO
        POP AF
        RETN
nmi__chk_in_nmi__NO:
        INC A                     ; we're not. record that we are
        LD (nmi__in_nmi), A

        PUSH HL                   ; inc clock
        PUSH DE                   ; AF has already been pushed
        PUSH BC
        LD B, 4
        LD DE, clock_32_bits
        LD HL, _32_bit_little_endian_1
        CALL add_int

        ;   ... process switch? ...

        POP BC
        POP DE
        POP HL
        LD A, 0                   ; record that we're no longer processing an NMI
        LD (nmi__in_nmi), A
        POP AF
        RETN
nmi__in_nmi:
        DEFB 0x00
_32_bit_little_endian_1:
        DEFW 1
        DEFW 0
shell_name:
        DEFB "shell.cmd", 0x00
os_readport:
os_exec:
        POP HL       ; pointer to the name of the command to execute
        OSFUNC_EXIT

